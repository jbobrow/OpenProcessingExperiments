

SpriteObject windows;
Predator apple;

void setup()
{
  size(600, 600);
  windows = new SpriteObject("windows.jpg");
  windows.x = width/2;
  windows.y = height/2;
  windows.s = 0.25;
  windows.speed = 5;
  windows.directionX = 1;
  windows.directionY = -3;
  apple = new Predator("apple.jpg");
  apple.s = 0.1;
  apple.speed = 20;
  apple.targetX = windows.x;
  apple.targetY = windows.y;
}

void draw()
{
  background(0);
  windows.update();
  windows.render();
  apple.targetX = windows.x;
  apple.targetY = windows.y;
  apple.update();
  apple.render();
}
class Predator extends SpriteObject
{
  float targetX;
  float targetY;
  
  Predator(String filename)
  {
    super(filename);
  }
  
  void update()
  {
    float dx = targetX - x;
    float dy = targetY - y;
    
    if(abs(dx) > 2)
    {
      x += dx / speed;
    }
    
    if(abs(dy) > 2)
    {
      y += dy / speed;
    }
  }
}
class SpriteObject
{
  float x = 0.0;
  float y = 0.0;
  float directionX;
  float directionY;
  float speed;
  float w = -1.0;
  float h = -1.0;
  float rotation = 0.0;
  float s = 1.0;
  PImage img;
  boolean loaded = false;
     
  SpriteObject(String filename)
  {
    this.img = requestImage(filename);
  }
    
  boolean isMouseOverlapping()
  {
    if (mouseX > (this.x - (this.w * 0.5 * this.s))
      && mouseX < (this.x + (this.w * 0.5 * this.s))
      && mouseY > (this.y - (this.h * 0.5 * this.s))
      && mouseY < (this.y + (this.h * 0.5 * this.s)))
    {
      return true;
    }
    else
    {
      return false;
    }
  }
    
  boolean isOverlapping(SpriteObject other)
  {
    float left = this.x - (this.w * 0.5 * this.s);
    float right = this.x + (this.w * 0.5 * this.s);
    float top = this.y - (this.h * 0.5 * this.s);
    float bottom = this.y + (this.h * 0.5 * this.s);
       
    float otherLeft = other.x - (other.w * 0.5 * other.s);
    float otherRight = other.x + (other.w * 0.5 * other.s);
    float otherTop = other.y - (other.h * 0.5 * other.s);
    float otherBottom = other.y + (other.h * 0.5 * other.s); 
       
    return !(left > otherRight || right < otherLeft || top > otherBottom || bottom < otherTop);
  }
    
  boolean isOverlappingRect(float otherLeft, float otherTop, float rwidth, float rheight)
  {
   float left = this.x - (this.w * 0.5 * this.s);
    float right = this.x + (this.w * 0.5 * this.s);
    float top = this.y - (this.h * 0.5 * this.s);
    float bottom = this.y + (this.h * 0.5 * this.s);
      
    float otherRight = otherLeft + rwidth;
    float otherBottom = otherTop + rheight;
    return !(left > otherRight || right < otherLeft || top > otherBottom || bottom < otherTop);
  }
   
  void update()
  {
    // If hitting Right
    if (this.x > width - (this.w * this.s * 0.5) && this.directionX > 0)
    {
      this.x = width - (this.w * this.s * 0.5);
      speed += 2;
      directionX = -directionX;
    }
     
    // Left
    if (this.x < (this.w * this.s * 0.5) && this.directionX <= 0)
    {
      this.x = (this.w * this.s * 0.5);
      speed -= 2;
      directionX = -directionX;
    }
     
    // Bottom
    if (this.y > height - (this.h * this.s * 0.5) && this.directionY > 0)
    {
      this.y = height - (this.h * this.s * 0.5);
      this.s -= 0.25;
      directionY = -directionY;
    }
     
    // Top
    if (this.y < (this.h * this.s * 0.5) && this.directionY <= 0)
    {
      this.y = (this.h * this.s * 0.5);
      this.s += 0.25;
      directionY = -directionY;
    }
     
    // Move the sprite
    this.y += directionY * speed;
    this.x += directionX * speed;
    //waveCounter++;
    float waveCounter = this.x * 4;
    this.y += sin(radians(waveCounter)) * 10;
    //this.x += random(-10, 10);
  }
 
  
     
  void render()
  {
    if (this.img.width > 0 && loaded == false)
    {
      loaded = true;
      if (this.w == -1.0)
      {
        this.w = this.img.width;
      }
      if (this.h == -1.0)
      {
        this.h = this.img.height;
      }
    }
       
    if (loaded)
    {
      imageMode(CENTER);
      pushMatrix();
      translate(this.x, this.y);
      rotate(radians(this.rotation));
      scale(this.s);
      image(this.img, 0, 0, this.w, this.h);
      popMatrix();
    }
  }
}



